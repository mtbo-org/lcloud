plugins {
    id 'java-library'
    id('com.diffplug.spotless') version "7.0.4"
    id 'signing'
}

def javaProjects = [
        project(":lcloud-common"),
        project(":lcloud-sql-discovery"),
        project(":lcloud-udp-discovery"),
        project(":lcloud-discovery-example"),
]


ext {
    assertjVersion = '3.27.3'
    junitVersion = '5.12.2'
    logbackVersion = '1.5.18'
    mockitoVersion = '5.17.0'
    r2dbcPostgresVersion = '1.0.7.RELEASE'
    r2dbcVersion = '3.5.0'
    reactorVersion = '3.8.0-M3'
    slf4jdk14version = '2.0.16'
}


configure(javaProjects) {
    group = 'org.mtbo.lcloud'

    apply plugin: 'java-library'

    sourceCompatibility = 21
    targetCompatibility = 21

    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            // Use the default importOrder configuration
            importOrder()

            removeUnusedImports()

            // Cleanthat will refactor your code, but it may break your style: apply it before your formatter
            cleanthat()          // has its own section below


            googleJavaFormat()   // has its own section below

            formatAnnotations()  // fixes formatting of type annotations, see below

            licenseHeaderFile rootProject.rootDir.toString() + '/license.header.txt'
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(GenerateModuleMetadata).tap {
        configureEach {
            enabled = false
        }
    }
}

